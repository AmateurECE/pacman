# Maintainer: Ethan D. Twardy <ethan.twardy@gmail.com>

pkgname=llhttp
pkgver=6.0.9
pkgrel=1
pkgdesc='Port of http_parser to llparse'
arch=('x86_64' 'aarch64')
url='https://llhttp.org'
license=('MIT')
depends=('glibc')
makedepends=('cmake' 'ts-node' 'npm' 'clang')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nodejs/llhttp/archive/v$pkgver.tar.gz"
    "0001-Add-pkg-config-file.patch")
sha256sums=('afb1b712bf36d99795ad932826598557f5f5a8822dbd8e507ac04f975bcc5b27'
            '4a7b61c40a18883b27211e1709aef7bc73f97d7e4a3583433471d34f77bd3620')

prepare() {
    patch --directory="$pkgname-$pkgver" --forward --strip=1 \
          --input="${srcdir}/0001-Add-pkg-config-file.patch"
}

build() {
    cd "$pkgname-$pkgver"
    npm install
    make PREFIX=/usr
}

package() {
    cd "$pkgname-$pkgver"
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE-MIT
}
